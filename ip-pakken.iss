; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{16A559DB-7E90-42DF-B59D-E6F877FB9766}
AppName=MosML og emacs
AppVersion=2.013
;AppVerName=MosML + emacs 2
AppPublisher=DIKU
AppPublisherURL=http://diku.dk
AppSupportURL=http://diku.dk
AppUpdatesURL=http://diku.dk
DefaultDirName={pf}\MosMLogEmacs
DefaultGroupName=MosML og emacs
OutputBaseFilename=MosMLogEmacs
Compression=lzma
SolidCompression=yes
ChangesEnvironment=yes

[Languages]
Name: "da"; MessagesFile: "compiler:Languages\Danish.isl"
Name: "en"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "mosml\*"; DestDir: "{app}\mosml"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: mosml
Source: "emacs-24.3\*"; DestDir: "{app}\emacs-24.3"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: emacs
Source: "User's Personal Data Folder\*"; DestDir: "{%HOME|{userappdata}}"; Flags: onlyifdoesntexist ignoreversion uninsneveruninstall recursesubdirs createallsubdirs; Components: emacs
Source: "setenv.exe"; DestDir: "{app}"; Flags: ignoreversion

[Dirs]
; tmp-dir for SML-mode
Name: "{localappdata}\emacs\smltmp"; Components: emacs

[Components]
Name: "mosml"; Description: "Moscow ML v2.10"; Flags: fixed; Types: full custom
Name: "emacs"; Description: "emacs v24.3"; Types: full custom

[Types]
Name: "full"; Description: "{cm:FullInstall}"
Name: "custom"; Description: "{cm:CustomInstall}"; Flags: iscustom

[Tasks]
Name: startmenu; Description: "{cm:StartMenu}"
Name: desktopicon; Description: "{cm:DesktopIcon}"

[Icons]
Name: "{group}\{cm:MosMLName}"; Filename: "{app}\mosml\bin\mosml.exe"; Parameters: "-P full"; Components: mosml; Tasks: startmenu
Name: "{group}\{cm:EmacsName}"; Filename: "{app}\emacs-24.3\bin\runemacs.exe"; Components: emacs; Tasks: startmenu
Name: "{group}\{cm:UninstallProgram,MosML + emacs}"; Filename: "{uninstallexe}"; Tasks: startmenu
Name: "{commondesktop}\{cm:MosMLName}"; Filename: "{app}\mosml\bin\mosml.exe"; Parameters: "-P full"; Components: mosml; Tasks: desktopicon
Name: "{commondesktop}\{cm:EmacsName}"; Filename: "{app}\emacs-24.3\bin\runemacs.exe"; Components: emacs; Tasks: desktopicon

[CustomMessages]
en.FullInstall=Full installation
da.FullInstall=Fuld installation
en.CustomInstall=Custom installation
da.CustomInstall=Brugerdefineret installation

en.StartMenu=Create Start Menu shortcuts
da.StartMenu=Lav genveje i startmenuen
en.DesktopIcon=Create shortcuts on your desktop
da.DesktopIcon=Lav genveje på skrivebordet

en.MosMLName=MosML Commandline
da.MosMLName=MosML Kommandolinje
en.EmacsName=emacs 24.3 with SML-mode
da.EmacsName=emacs 24.3 med SML-mode
en.UninstallProgram=Uninstall %1
da.UninstallProgram=Afinstallér %1

en.Updating=Updating %1...
da.Updating=Opdaterer %1...
en.Setting=Setting %1...
da.Setting=Sætter %1...
da.Deleting=Sletter %1...
en.Deleting=Deleting %1...

[Run]
Filename: "{app}\setenv.exe"; Parameters: "-a PATH %""{app}\mosml\bin"""; StatusMsg: "{cm:Updating,PATH}"; Flags: runhidden; Components: mosml
Filename: "{app}\setenv.exe"; Parameters: "-a MOSMLLIB ""{app}\mosml\lib\mosml"""; StatusMsg: "{cm:Setting,MOSMLLIB}"; Flags: runhidden; Components: mosml

[UninstallRun]
Filename: "{app}\setenv.exe"; Parameters: "-d PATH %""{app}\mosml\bin"""; StatusMsg: "{cm:Updating,PATH}"; Flags: runhidden; Components: mosml
Filename: "{app}\setenv.exe"; Parameters: "-d MOSMLLIB"; StatusMsg: "{cm:Deleting,MOSMLLIB}"; Flags: runhidden; Components: mosml


